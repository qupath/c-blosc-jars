plugins {
    id 'maven-publish'
}

group = 'io.github.qupath'
version = '1.21.5-SNAPSHOT'


publishing {
    repositories {
        maven {
            name = "SciJava"
            def releasesRepoUrl = uri("https://maven.scijava.org/content/repositories/releases")
            def snapshotsRepoUrl = uri("https://maven.scijava.org/content/repositories/snapshots")
            // Use gradle -Prelease publish
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId group
            version version
            artifact (file("blosc-natives.jar"))
            artifact (file("blosc-natives-darwin-aarch64.jar")) {
                classifier "darwin-aarch64"
            }
            artifact (file("blosc-natives-darwin-x86-64.jar")) {
                classifier "darwin-x86_64"
            }
            artifact (file("blosc-natives-win32-x86-64.jar")) {
                classifier "win32-x86_64"
            }
            artifact (file("blosc-natives-linux-x86-64.jar")) {
                classifier "linux-x86_64"
            }
            pom {
                licenses {
                    license {
                        name = 'LGPL'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
                    }
                }
            }
        }
    }
}
